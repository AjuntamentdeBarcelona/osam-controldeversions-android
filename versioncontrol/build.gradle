apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 145
        versionName "1.4.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

// get version name
def getVersionName() {
    if(hasProperty('snapshot') && property('snapshot') == true) {
        // property value doesn't matter
        println "********* deploying snapshot **********";
        return android.defaultConfig.versionName + "-SNAPSHOT";
    } else {
        println "********* deploying release **********";
        return android.defaultConfig.versionName;
    }
}

uploadArchives {
    repositories {
        // the maven plugin features this deployer
        mavenDeployer {
            // releases repository
            repository(url: T21_NEXUS_URL + T21_NEXUS_REPO_RELEASE) {
                authentication(userName: T21_NEXUS_USERNAME, password: T21_NEXUS_PASSWORD)
            }
            // snapshot repository - when version name contains '*-SNAPSHOT' this repository is used
            snapshotRepository(url: T21_NEXUS_URL + T21_NEXUS_REPO_SNAPSHOT) {
                authentication(userName: T21_NEXUS_USERNAME, password: T21_NEXUS_PASSWORD)
            }

            // pom project information
            pom.project {
                version getVersionName()
                groupId 'com.tempos21.versioncontrol'
                artifactId 'versioncontrol_android'
                name 'Version Control Module'
                packaging 'aar'
            }
        }
    }
}

task install(dependsOn: uploadArchives)

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'org.parceler:parceler-api:1.1.6'
    apt 'org.parceler:parceler:1.1.6'
}
